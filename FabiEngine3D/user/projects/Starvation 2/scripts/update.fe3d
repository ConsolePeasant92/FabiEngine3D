18 51
META script_type_update
META execution_entry

/// Input
BOOL up = fe3d:input_key_down("KEY_SPACE")
BOOL forward = fe3d:input_key_down("KEY_W")
BOOL backward = fe3d:input_key_down("KEY_S")
BOOL left = fe3d:input_key_down("KEY_A")
BOOL right = fe3d:input_key_down("KEY_D")
BOOL shift = fe3d:input_key_down("KEY_LSHIFT")

/// Running speed
CAST shift INT
INCR shift 1
CAST shift DEC
DEC movingSpeed = _baseSpeed
MUL movingSpeed shift

STR selectedID = fe3d:raycast_game_entity_group("tree7", "trunk")
IF selectedID NOT "":
    VEC3 camPos = fe3d:camera_position_get()
    VEC3 selectedPos = fe3d:entity_game_position_get(selectedID)
    DEC distance = math:distance(camPos, selectedPos)
    IF distance LESS 5.0:
        fe3d:entity_game_delete(selectedID)

/// Jumping movement
IF up IS <true>:
    fe3d:camera_move(0.0, movingSpeed, 0.0)

/// WASD movement
IF forward IS <true>:
    fe3d:camera_follow_z(movingSpeed)
IF backward IS <true>:
    NEG movingSpeed
    fe3d:camera_follow_z(movingSpeed)
    NEG movingSpeed
IF left IS <true>:
    NEG movingSpeed
    fe3d:camera_follow_x(movingSpeed)
    NEG movingSpeed
IF right IS <true>:
    fe3d:camera_follow_x(movingSpeed)
