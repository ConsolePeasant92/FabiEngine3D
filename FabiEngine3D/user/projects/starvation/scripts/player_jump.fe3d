10 43
META script_type_update

/// Temporary values
BOOL jump = fe3d:input_is_key_pressed("KEY_SPACE")
VEC3 camPos = fe3d:camera_get_position()
STR yCollision = fe3d:collision_check_camera_models("Y")

/// Initiate jump
IF _isToolPlaying IS <false> AND _isPickupPlaying IS <false> AND _stamina MORE 19.0:
    IF jump IS <true> AND _isJumping IS <false> AND _mayJump IS <true>:
        IF _isUnderWater IS <false> AND _isTalking IS <false>:
            EDIT _isJumping = <true>
            EDIT _mayJump = <false>
            DECR _stamina 20.0

/// Jumping movement
IF _isJumping IS <true>:
    IF _jumpingSpeed LESS 0.01:
        EDIT _isJumping = <false>
        EDIT _jumpingSpeed = 0.1
    ELIF yCollision NOT "" AND jump IS <false>:
        EDIT _isJumping = <false>
        EDIT _jumpingSpeed = 0.1
        fe3d:camera_move(0.0, -0.1, 0.0)
    ELSE:
        MUL _jumpingSpeed 0.95
        fe3d:camera_move(0.0, _jumpingSpeed, 0.0)
ELSE:
    BOOL underGround = fe3d:collision_is_camera_under_terrain()
    IF underGround IS <true> OR yCollision NOT "":
        EDIT _fallingSpeed = 0.05
        EDIT _mayJump = <true>
        EDIT _isFalling = <false>
    ELSE:
        MUL _fallingSpeed 1.015
        NEG _fallingSpeed
        fe3d:camera_move(0.0, _fallingSpeed, 0.0)
        NEG _fallingSpeed
        EDIT _isFalling = <true>
