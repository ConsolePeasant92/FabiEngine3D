83 59
META script_type_update

/// Temporary values
INT scrollDir = fe3d:input_get_mousewheel_direction()
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL cPressed = fe3d:input_is_key_pressed("KEY_C")
BOOL rPressed = fe3d:input_is_key_pressed("KEY_R")

/// Opening or closing building menu
IF cPressed IS <true> AND _isCrafting IS <false>:
    IF _isBuilding IS <false>:
        EDIT _isBuilding = <true>
    ELSE:
        EDIT _isBuilding = <false>

/// Update rotation
IF _isBuilding IS <true> AND rPressed IS <true>:
    IF _buildRotation IS 270.0:
        EDIT _buildRotation = 0.0
    ELSE:
        INCR _buildRotation 90.0

/// Hide previous building
fe3d:model_set_visible(_buildables[_buildIndex], <false>)

/// Update building choosing
IF _isBuilding IS <true>:
    IF scrollDir IS -1:
        IF _buildIndex IS 0:
            EDIT _buildIndex = _maxBuildIndex
        ELSE:
            DECR _buildIndex 1
    ELIF scrollDir IS 1:
        IF _buildIndex IS _maxBuildIndex:
            EDIT _buildIndex = 0
        ELSE:
            INCR _buildIndex 1

/// Show current building
IF _isBuilding IS <true>:
    fe3d:model_set_visible(_buildables[_buildIndex], <true>)

/// Update building position & rotation
IF _isBuilding IS <true>:
    VEC3 buildingPos = fe3d:camera_get_position()
    IF _buildRotation IS 0.0:
        DECR buildingPos.x _buildOffsets[_buildIndex]
    ELIF _buildRotation IS 90.0:
        INCR buildingPos.z _buildOffsets[_buildIndex]
    ELIF _buildRotation IS 180.0:
        INCR buildingPos.x _buildOffsets[_buildIndex]
    ELIF _buildRotation IS 270.0:
        DECR buildingPos.z _buildOffsets[_buildIndex]
    fe3d:model_set_position(_buildables[_buildIndex], buildingPos.x, 0.0, buildingPos.z)
    fe3d:model_set_rotation(_buildables[_buildIndex], 0.0, _buildRotation, 0.0)

/// Update building validation color
IF _isBuilding IS <true>:
    STR valid = fe3d:collision_check_model_models(_buildables[_buildIndex], "", "", "")
    IF valid NOT "":
        fe3d:model_set_color(_buildables[_buildIndex], 1.0, 0.0, 0.0)
    ELSE:
        fe3d:model_set_color(_buildables[_buildIndex], 0.0, 1.0, 0.0)

/// Update building placement
IF _isBuilding IS <true> AND lmbPressed IS <true>:
    IF valid IS "":
        INT counter = _buildCounters[_buildIndex]
        CAST counter STR
        STR newID = misc:concat_strings(_buildables[_buildIndex], counter)
        LIST temp = {"house14","house2","house13","storage","well2","pen"}
        STR previewID = temp[_buildIndex]
        fe3d:model_place(newID, previewID, buildingPos.x, 0.0, buildingPos.z)
        fe3d:model_set_rotation(newID, 0.0, _buildRotation, 0.0)
        DEC height = _buildHeights[_buildIndex]
        MUL height 0.1
        fe3d:model_set_max_height(newID, height)
        INCR _buildCounters[_buildIndex] 1

/// AABB heights
EDIT _buildHeights = {}
VEC3 smallHeight = fe3d:aabb_get_size("small_house_display_front_left")
VEC3 mediumHeight = fe3d:aabb_get_size("medium_house_display_front")
VEC3 bigHeight = fe3d:aabb_get_size("big_house_display_back")
VEC3 storageHeight = fe3d:aabb_get_size("storage_display_main")
VEC3 wellHeight = fe3d:aabb_get_size("well_display_well")
VEC3 penHeight = fe3d:aabb_get_size("pen_display_front")
PUSH _buildHeights smallHeight.y
PUSH _buildHeights mediumHeight.y
PUSH _buildHeights bigHeight.y
PUSH _buildHeights storageHeight.y
PUSH _buildHeights wellHeight.y
PUSH _buildHeights penHeight.y
