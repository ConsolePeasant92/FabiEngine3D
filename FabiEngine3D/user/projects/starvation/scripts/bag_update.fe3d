10 47
META script_type_update

/// Check if player wants to open/close bag
BOOL bToggled = fe3d:input_is_key_toggled("KEY_B")
IF bToggled IS <true>:
    fe3d:image_set_visible("bag_open", <true>)
    fe3d:image_set_visible("scroll_bag", <true>)
    fe3d:image_set_visible("bag_closed", <false>)
ELSE:
    fe3d:image_set_visible("bag_open", <false>)
    fe3d:image_set_visible("scroll_bag", <false>)
    fe3d:image_set_visible("bag_closed", <true>)

/// Update bag items visibility
fe3d:image_set_visible("wood", bToggled)
fe3d:text_set_visible("wood_count", bToggled)
fe3d:image_set_visible("stone", bToggled)
fe3d:text_set_visible("stone_count", bToggled)
fe3d:image_set_visible("clay", bToggled)
fe3d:text_set_visible("clay_count", bToggled)
fe3d:image_set_visible("seeds", bToggled)
fe3d:text_set_visible("seeds_count", bToggled)
fe3d:image_set_visible("corn", bToggled)
fe3d:text_set_visible("corn_count", bToggled)
fe3d:image_set_visible("fish", bToggled)
fe3d:text_set_visible("fish_count", bToggled)
fe3d:image_set_visible("meat", bToggled)
fe3d:text_set_visible("meat_count", bToggled)
fe3d:image_set_visible("iron", bToggled)
fe3d:text_set_visible("iron_count", bToggled)
fe3d:image_set_visible("coin", bToggled)
fe3d:text_set_visible("coin_count", bToggled)

/// Calculate total bag items AND show bag text
LIST textNames = {"wood_count","stone_count","clay_count","seeds_count","corn_count"}
PUSH textNames "fish_count"
PUSH textNames "meat_count"
PUSH textNames "iron_count"
PUSH textNames "coin_count"
INT size = misc:get_list_size("_bag")
INT index = 0
INT itemCount = 0
INT strSize = 0
DEC weight = 0.0
EDIT _totalBagWeight = 0.0
LOOP:
    IF index IS size:
        BREAK
    ELSE:
        EDIT itemCount = _bag[index]
        CAST itemCount DEC
        EDIT weight = itemCount
        MUL weight _bagWeights[index]
        INCR _totalBagWeight weight
        CAST itemCount INT
        CAST itemCount STR
        EDIT strSize = misc:get_string_size(itemCount)
        IF strSize IS 1:
            EDIT itemCount = misc:concat_strings("00", itemCount)
        ELIF strSize IS 2:
            EDIT itemCount = misc:concat_strings("0", itemCount)
        fe3d:text_set_content(textNames[index], itemCount)
        CAST itemCount INT
        INCR index 1

/// Update bag icon color
DEC totalWeight = _totalBagWeight
DIV totalWeight _maxBagWeight
DEC inverseColor = 1.0
DECR inverseColor totalWeight
fe3d:image_set_color("bag_open", 1.0, inverseColor, inverseColor)

/// Update bag weight text
BOOL isBagOpen = fe3d:image_is_visible("bag_open")
fe3d:text_set_visible("bag_weight", isBagOpen)
EDIT totalWeight = _totalBagWeight
CAST totalWeight STR
EDIT totalWeight = misc:get_string_part(totalWeight, 0, 4)
EDIT totalWeight = misc:concat_strings(totalWeight, "KG")
fe3d:text_set_content("bag_weight", totalWeight)
