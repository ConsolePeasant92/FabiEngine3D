38 45
META script_type_update

/// Temporary values
BOOL up = fe3d:input_is_key_down("KEY_W")
BOOL down = fe3d:input_is_key_down("KEY_S")
BOOL left = fe3d:input_is_key_down("KEY_A")
BOOL right = fe3d:input_is_key_down("KEY_D")
BOOL shift = fe3d:input_is_key_down("KEY_LSHIFT")
EDIT _isWalking = (up IS <true> OR down IS <true> OR left IS <true> OR right IS <true>)
DEC accSpeed = 0.0001
DEC minSpeed = -0.025
DEC maxSpeed = 0.025

/// Running speed
IF shift IS <true> AND up IS <true>:
    MUL minSpeed 2.0
    MUL maxSpeed 2.0
    EDIT _isRunning = <true>
ELSE:
    EDIT _isRunning = <false>

/// WASD movement
IF left IS <true>:
    DECR _movingAcc.x accSpeed
IF right IS <true>:
    INCR _movingAcc.x accSpeed
IF up IS <true>:
    INCR _movingAcc.z accSpeed
IF down IS <true>:
    DECR _movingAcc.z accSpeed

/// Update acceleration
IF left IS <false> AND _movingAcc.x LESS 0.0:
    EDIT _movingAcc.x = 0.0
IF right IS <false> AND _movingAcc.x MORE 0.0:
    EDIT _movingAcc.x = 0.0
IF up IS <false> AND _movingAcc.z MORE 0.0:
    EDIT _movingAcc.z = 0.0
IF down IS <false> AND _movingAcc.z LESS 0.0:
    EDIT _movingAcc.z = 0.0

/// Move camera
INCR _movingSpeed _movingAcc
MUL _movingSpeed 0.9
EDIT _movingSpeed.x = math:clamp(_movingSpeed.x, minSpeed, maxSpeed)
EDIT _movingSpeed.z = math:clamp(_movingSpeed.z, minSpeed, maxSpeed)
fe3d:camera_follow_x(_movingSpeed.x)
fe3d:camera_follow_z(_movingSpeed.z)
