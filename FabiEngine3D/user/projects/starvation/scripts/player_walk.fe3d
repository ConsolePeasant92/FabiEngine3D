22 22
META script_type_update

/// Temporary values
BOOL up = fe3d:input_is_key_down("KEY_W")
BOOL down = fe3d:input_is_key_down("KEY_S")
BOOL left = fe3d:input_is_key_down("KEY_A")
BOOL right = fe3d:input_is_key_down("KEY_D")
BOOL shift = fe3d:input_is_key_down("KEY_LSHIFT")
EDIT _isWalking = (up IS <true> OR down IS <true> OR left IS <true> OR right IS <true>)
DEC accSpeed = 0.0001
DEC minSpeed = -0.0025
DEC maxSpeed = 0.0025

/// Movement must be slowed if underwater
IF _isUnderWater IS <true>:
    MUL minSpeed 0.5
    MUL maxSpeed 0.5

/// Determine max movement speed
IF shift IS <true> AND up IS <true>:
    IF _stamina MORE 0.0 AND _isUnderWater IS <false>:
        MUL minSpeed 2.0
        MUL maxSpeed 2.0
        EDIT _isRunning = <true>
        DECR _stamina 0.1
    ELSE:
        EDIT _isRunning = <false>
ELSE:
    EDIT _isRunning = <false>
    IF _stamina LESS 100.0:
        INCR _stamina 0.1

/// WASD movement
IF left IS <true> AND _movingAcc.x MORE minSpeed:
    DECR _movingAcc.x accSpeed
IF right IS <true> AND _movingAcc.x LESS maxSpeed:
    INCR _movingAcc.x accSpeed
IF up IS <true> AND _movingAcc.z LESS maxSpeed:
    INCR _movingAcc.z accSpeed
IF down IS <true> AND _movingAcc.z MORE minSpeed:
    DECR _movingAcc.z accSpeed

/// Update acceleration
IF left IS <false> AND _movingAcc.x LESS 0.0:
    EDIT _movingAcc.x = 0.0
IF right IS <false> AND _movingAcc.x MORE 0.0:
    EDIT _movingAcc.x = 0.0
IF up IS <false> AND _movingAcc.z MORE 0.0:
    EDIT _movingAcc.z = 0.0
IF down IS <false> AND _movingAcc.z LESS 0.0:
    EDIT _movingAcc.z = 0.0

/// Move camera
INCR _movingSpeed _movingAcc
MUL _movingAcc 0.99
MUL _movingSpeed 0.9

/// Only if no tool or pickup animations are playing
IF _isToolPlaying IS <false> AND _isPickupPlaying IS <false> AND _isTalking IS <false>:
    fe3d:camera_follow_x(_movingSpeed.x)
    fe3d:camera_follow_z(_movingSpeed.z)
