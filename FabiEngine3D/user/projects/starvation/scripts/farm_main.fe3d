80 0
META script_type_update

/// Temporary values
STR toolID = _inventory[_equipIndex]
STR wheatAabbID = ""
VEC3 camPos = fe3d:camera_get_position()
VEC3 wheatSize = [0.0 0.0 0.0]
DEC wheatHeight = 0.0
DEC pitch = fe3d:camera_get_pitch()
DEC feetY = fe3d:terrain_get_pixel_height(camPos.x, camPos.z)
DEC waterY = fe3d:water_get_height()
BOOL isLookingDown = <false>
BOOL isHovered = <false>
INT leastWheatCount = _wheatCount
DECR leastWheatCount 1

/// Update plowing hover
fe3d:text_set_visible("plow", <false>)
IF feetY MORE waterY AND camPos.y LESS 34.0 AND pitch LESS -25.0:
    IF toolID IS "hoe" AND _isInvOpen IS <true> AND _bag[3] MORE leastWheatCount:
        fe3d:text_set_visible("plow", <true>)
        EDIT isLookingDown = <true>

/// Update wheat plowing
IF _wasHoePlaying IS <true> AND _isHoePlaying IS <false>:
    IF isLookingDown IS <true>:
        /// Compose ID of wheat model
        INT newID = misc:get_unique_integer(0, 1000)
        CAST newID STR
        EDIT newID = misc:concat_strings("wheat", newID)
        /// Place "seeds" of wheat model
        fe3d:model_place(newID, "wheat", camPos.x, feetY, camPos.z)
        fe3d:model_set_aabb_collision_responsive(newID, <false>)
        STR aabbID = misc:concat_strings(newID, "_wheat")
        VEC3 size = fe3d:aabb_get_size(aabbID)
        MUL size.y 0.025
        fe3d:model_set_max_height(newID, size.y)
        /// Plant the seeds
        DECR _bag[3] _wheatCount

/// Update wheat growing
LIST wheatIDs = fe3d:model_find_ids("wheat")
STR wheatID = ""
DEC speed = 0.0
INT length = misc:get_list_size("wheatIDs")
INT index = 0
LOOP:
    IF index IS length:
        BREAK
    ELSE:
        EDIT wheatID = wheatIDs[index]
        EDIT wheatAabbID = misc:concat_strings(wheatID, "_wheat")
        EDIT wheatSize = fe3d:aabb_get_size(wheatAabbID)
        EDIT wheatHeight = fe3d:model_get_max_height(wheatID)
        /// Check if wheat is not fully grown yet
        IF wheatHeight LESS wheatSize.y:
            EDIT speed = _wheatGrowingSpeed
            MUL speed _timeMultiplier
            INCR wheatHeight speed
            /// Grow the wheat
            fe3d:model_set_max_height(wheatID, wheatHeight)
        INCR index 1

/// Update wheat hover
STR hoveredID = fe3d:raycast_into_model("wheat", "", <true>)
DEC distance = fe3d:raycast_into_model_distance("wheat", "", <true>)
IF distance IS -1.0:
    EDIT distance = 1000.0
fe3d:text_set_visible("reap", <false>)
IF hoveredID NOT "":
    /// Check if wheat is fully grown and looking at it
    EDIT wheatAabbID = misc:concat_strings(hoveredID, "_wheat")
    EDIT wheatSize = fe3d:aabb_get_size(wheatAabbID)
    EDIT wheatHeight = fe3d:model_get_max_height(hoveredID)
    IF distance LESS _maxReach AND wheatHeight MORE wheatSize.y:
        EDIT isHovered = <true>
        /// Make the wheat reapable
        IF toolID IS "scythe":
            IF _isInvOpen IS <true>:
                fe3d:text_set_visible("reap", <true>)

/// Update wheat reaping
IF isHovered IS <true> AND _wasScythePlaying IS <true> AND _isScythePlaying IS <false>:
    fe3d:model_delete(hoveredID)
    INCR _bag[4] _wheatCount
