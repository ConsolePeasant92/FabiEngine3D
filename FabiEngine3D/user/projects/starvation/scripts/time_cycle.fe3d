16 22
META script_type_update

/// Update time passing
IF _passedTime MORE 100.0:
    EDIT _passedTime = 0.0
    EDIT _isTimeCycling = <true>
    IF _mustBeDayTime IS <true>:
        EDIT _mustBeDayTime = <false>
    ELSE:
        EDIT _mustBeDayTime = <true>
ELSE:
    IF _isTimeCycling IS <false>:
        INCR _passedTime _timeSpeed

/// Update day-night cycle
DEC ambSpeed = 0.00005
DEC dirSpeed = 0.00005
DEC skySpeed = 0.0001
DEC heightSpeed = 0.25
MUL ambSpeed _timeMultiplier
MUL dirSpeed _timeMultiplier
MUL skySpeed _timeMultiplier
MUL heightSpeed _timeMultiplier
IF _isTimeCycling IS <true>:
    IF _mustBeDayTime IS <true>:
        INCR _ambLightIntensity ambSpeed
        INCR _dirLightIntensity dirSpeed
        DECR _skyMixValue skySpeed
        IF _skyMixValue LESS 0.5:
            INCR _dirLightPos.y heightSpeed
            INCR _dirLightPos.z heightSpeed
        ELSE:
            DECR _dirLightPos.y heightSpeed
            DECR _dirLightPos.z heightSpeed
    ELSE:
        DECR _ambLightIntensity ambSpeed
        DECR _dirLightIntensity dirSpeed
        INCR _skyMixValue skySpeed
        IF _skyMixValue MORE 0.5:
            INCR _dirLightPos.y heightSpeed
            INCR _dirLightPos.z heightSpeed
        ELSE:
            DECR _dirLightPos.y heightSpeed
            DECR _dirLightPos.z heightSpeed
    IF _skyMixValue LESS 0.0 OR _skyMixValue MORE 1.0:
        EDIT _isTimeCycling = <false>
        IF _mustBeDayTime IS <true>:
            fe3d:graphics_disable_light_mapping()
            fe3d:graphics_disable_point_lighting()
            EDIT _areLightsOn = <false>
            EDIT _isDayTime = <true>
            EDIT _skyMixValue = 0.0
            EDIT _ambLightIntensity = _initAmbLightIntensity
            EDIT _dirLightIntensity = _initDirLightIntensity
            EDIT _dirLightPos = _initDirLightPos
        ELSE:
            fe3d:graphics_enable_light_mapping()
            fe3d:graphics_enable_point_lighting()
            EDIT _areLightsOn = <true>
            EDIT _isDayTime = <false>

/// Apply cycle values
DEC fogIntensity = 0.5
DEC fogDecreaser = 0.4
MUL fogDecreaser _skyMixValue
DECR fogIntensity fogDecreaser
VEC3 dirPos = _dirLightPos
fe3d:sky_set_mix_value(_skyMixValue)
fe3d:graphics_set_ambient_light_intensity(_ambLightIntensity)
fe3d:graphics_set_directional_light_intensity(_dirLightIntensity)
fe3d:graphics_set_directional_light_position(dirPos.x, dirPos.y, dirPos.z)
IF _isUnderWater IS <false>:
    fe3d:graphics_set_fog_color(fogIntensity, fogIntensity, fogIntensity)

/// Update fire billboards
CONST LIST billboards = fe3d:billboard_find_ids("fire")
INT index = 0
INT size = misc:get_list_size("billboards")
LOOP:
    IF index IS size:
        BREAK
    fe3d:billboard_set_visible(billboards[index], _areLightsOn)
    INCR index 1
