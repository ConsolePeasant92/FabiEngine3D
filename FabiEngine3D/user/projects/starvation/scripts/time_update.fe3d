66 0
META script_type_update

/// Update time passing
IF _passedTime MORE 100.0:
    EDIT _passedTime = 0.0
    EDIT _isTimeCycling = <true>
    IF _mustBeDayTime IS <true>:
        EDIT _mustBeDayTime = <false>
    ELSE:
        EDIT _mustBeDayTime = <true>
ELSE:
    IF _isTimeCycling IS <false>:
        DEC timeSpeed = 0.01
        MUL timeSpeed _timeMultiplier
        INCR _passedTime timeSpeed

/// Update day-night cycle
DEC ambSpeed = 0.00015
DEC dirSpeed = 0.0001225
DEC skySpeed = 0.00025
DEC heightSpeed = 0.35
MUL ambSpeed _timeMultiplier
MUL dirSpeed _timeMultiplier
MUL skySpeed _timeMultiplier
MUL heightSpeed _timeMultiplier
IF _isTimeCycling IS <true>:
    IF _mustBeDayTime IS <true>:
        INCR _ambLightIntensity ambSpeed
        INCR _dirLightIntensity dirSpeed
        DECR _skyMixValue skySpeed
        IF _skyMixValue LESS 0.5:
            INCR _dirLightPos.y heightSpeed
        ELSE:
            DECR _dirLightPos.y heightSpeed
    ELSE:
        DECR _ambLightIntensity ambSpeed
        DECR _dirLightIntensity dirSpeed
        INCR _skyMixValue skySpeed
        IF _skyMixValue MORE 0.5:
            INCR _dirLightPos.y heightSpeed
        ELSE:
            DECR _dirLightPos.y heightSpeed
    IF _skyMixValue LESS 0.0 OR _skyMixValue MORE 1.0:
        EDIT _isTimeCycling = <false>
        IF _mustBeDayTime IS <true>:
            fe3d:graphics_disable_light_mapping()
            fe3d:graphics_disable_point_lighting()
            EDIT _areLightsOn = <false>
            EDIT _isDayTime = <true>
        ELSE:
            fe3d:graphics_enable_light_mapping()
            fe3d:graphics_enable_point_lighting()
            EDIT _areLightsOn = <true>
            EDIT _isDayTime = <false>

/// Apply cycle values
DEC fogIntensity = 0.5
DEC fogDecreaser = 0.4
MUL fogDecreaser _skyMixValue
DECR fogIntensity fogDecreaser
VEC3 dirPos = _dirLightPos
fe3d:sky_set_mix_value(_skyMixValue)
fe3d:graphics_set_ambient_light_intensity(_ambLightIntensity)
fe3d:graphics_set_directional_light_intensity(_dirLightIntensity)
fe3d:graphics_set_directional_light_position(dirPos.x, dirPos.y, dirPos.z)
fe3d:graphics_set_fog_color(fogIntensity, fogIntensity, fogIntensity)

/// Update fire billboards
CONST LIST billboards = fe3d:billboard_find_ids("fire")
INT index = 0
INT size = misc:get_list_size("billboards")
LOOP:
    IF index IS size:
        BREAK
    fe3d:billboard_set_visible(billboards[index], _areLightsOn)
    INCR index 1
