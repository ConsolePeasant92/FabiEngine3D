20 40
META script_type_update

/// Update stone hover
fe3d:text_set_visible("mine", <false>)
BOOL isHovered = <false>
BOOL allowed = <true>
STR toolID = _inventory[_equipIndex]
STR hoveredID = fe3d:raycast_into_model("stone", "", <true>)
DEC distance = fe3d:raycast_into_model_distance("stone", "", <true>)
STR stonesException = fe3d:raycast_into_model("stones", "", <true>)
IF hoveredID NOT "" AND stonesException IS "":
    VEC3 stonePos = fe3d:model_get_position(hoveredID)
    STR subString = misc:get_string_part(hoveredID, 0, 6)
    IF subString IS "stone5":
        IF toolID NOT "pickaxe_iron":
            EDIT allowed = <false>
    IF distance LESS _maxReach AND allowed IS <true>:
        EDIT isHovered = <true>
        STR ps = "pickaxe_stone"
        STR pi = "pickaxe_iron"
        IF toolID IS ps OR toolID IS pi":
            IF _isInvOpen IS <true>:
                fe3d:text_set_visible("mine", <true>)

/// Check if mining animation finished
IF _wasPickaxePlaying IS <true> AND _isPickaxePlaying IS <false>:
    IF isHovered IS <true> AND allowed IS <true>:
        fe3d:model_delete(hoveredID)
        INT stonesID = misc:get_unique_integer(0, 1000)
        CAST stonesID STR
        EDIT stonesID = misc:concat_strings("stones", stonesID)
        PUSH _stonesIDs stonesID
        EDIT _fallingStonesID = stonesID
        EDIT _fallingStonesTargetHeight = stonePos.y
        INCR stonePos.y 5.0
        fe3d:model_place(stonesID, "stones", stonePos.x, stonePos.y, stonePos.z)
        IF subString IS "stone5":
            fe3d:model_set_size(stonesID, 1.2, 1.2, 1.2)
        DECR _durabilities[1] _durabilityHit

/// Stones falling updates
IF _fallingStonesID NOT "":
    VEC3 currentPos = fe3d:model_get_position(_fallingStonesID)
    IF currentPos.y MORE _fallingStonesTargetHeight:
        fe3d:model_move(_fallingStonesID, 0.0, -0.1, 0.0)
    ELSE:
        EDIT _fallingStonesID = ""

/// Stones picking updates
EDIT hoveredID = fe3d:raycast_into_model("stones", "", <true>)
EDIT distance = fe3d:raycast_into_model_distance("stones", "", <true>)
IF hoveredID NOT "" AND _wasPickupPlaying IS <true> AND _isPickupPlaying IS <false>:
    IF distance LESS _maxReach:
        fe3d:model_delete(hoveredID)
        INCR _bag[1] 6
