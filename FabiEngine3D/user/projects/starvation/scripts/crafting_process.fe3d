53 51
META script_type_update

/// Prepare crafting process
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL spacePressed = fe3d:input_is_key_pressed("KEY_SPACE")
STR confirm = fe3d:raycast_into_billboard("craft_confirm", <true>)
STR cancel = fe3d:raycast_into_billboard("craft_cancel", <true>)
BOOL confirmed = (confirm NOT "")
BOOL cancelled = (cancel NOT "")

/// Check if player has the required resources
IF _isCrafting IS <true> AND confirmed IS <true>:
    INT wp = _craftablesWoodPrice[_craftIndex]
    INT sp = _craftablesStonePrice[_craftIndex]
    INT ip = _craftablesIronPrice[_craftIndex]
    DECR wp 1
    DECR sp 1
    DECR ip 1
    EDIT confirmed = (_bag[0] MORE wp AND _bag[1] MORE sp AND _bag[7] MORE ip)

/// Buttons hovering
IF confirmed IS <true> AND _isCrafting IS <true>:
    fe3d:billboard_set_size("craft_confirm", 1.25, 1.25)
    fe3d:billboard_set_color("craft_confirm", 1.0, 1.0, 1.0)
ELSE:
    fe3d:billboard_set_size("craft_confirm", 1.0, 1.0)
    fe3d:billboard_set_color("craft_confirm", 0.0, 0.0, 0.0)
IF cancelled IS <true> AND _isCrafting IS <true>:
    fe3d:billboard_set_size("craft_cancel", 1.25, 1.25)
ELSE:
    fe3d:billboard_set_size("craft_cancel", 1.0, 1.0)

/// Update crafting menu - process
IF _isCrafting IS <true>:
    IF confirmed IS <true> AND lmbPressed IS <true>:
        IF _inventory[_equipIndex] NOT "":
            fe3d:model_set_visible(_inventory[_equipIndex], <false>)
        STR itemName = _craftables[_craftIndex]
        INT length = misc:get_string_size(itemName)
        DECR length 8
        EDIT itemName = misc:get_string_part(itemName, 0, length)
        BOOL existing = fe3d:model_is_existing(itemName)
        IF existing IS <false>:
            fe3d:model_place(itemName, itemName, 0.0, 0.0, 0.0)
            fe3d:model_set_aabb_responsive(itemName, <false>)
        EDIT _equipIndex = _craftablesIndex[_craftIndex]
        EDIT _inventory[_equipIndex] = itemName
        DECR _bag[0] _craftablesWoodPrice[_craftIndex]
        DECR _bag[1] _craftablesStonePrice[_craftIndex]
        DECR _bag[7] _craftablesIronPrice[_craftIndex]
        /// Iron tools have much longer durability
        INT i = _craftIndex
        IF i IS 1 OR i IS 3 OR i IS 5 OR i IS 7 OR i IS 8 OR i IS 9 OR i IS 11:
            EDIT _durabilities[_equipIndex] = 100.0
        ELSE:
            EDIT _durabilities[_equipIndex] = 1.0
    ELIF cancelled IS <true> AND lmbPressed IS <true>:
        EDIT _isCrafting = <false>
        fe3d:cursor_hide()
        fe3d:model_set_visible(_craftables[_craftIndex], <false>)
        fe3d:text_set_visible(_craftables[_craftIndex], <false>)
        fe3d:billboard_set_visible("craft_confirm", <false>)
        fe3d:billboard_set_visible("craft_cancel", <false>)
    ELIF spacePressed IS <true>:
        fe3d:model_set_visible(_craftables[_craftIndex], <false>)
        fe3d:text_set_visible(_craftables[_craftIndex], <false>)
        INT maxIndex = misc:get_list_size("_craftables")
        DECR maxIndex 1
        IF _craftIndex IS maxIndex:
            EDIT _craftIndex = 0
        ELSE:
            INCR _craftIndex 1
        fe3d:model_set_visible(_craftables[_craftIndex], <true>)
        fe3d:text_set_visible(_craftables[_craftIndex], <true>)
