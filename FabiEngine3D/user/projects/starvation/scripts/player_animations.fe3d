11 49
META script_type_update

/// First person player animation
VEC3 camPos = fe3d:camera_get_position()
DEC yaw = fe3d:camera_get_yaw()
NEG yaw
INCR yaw 90.0
DECR camPos.y 2.6
fe3d:model_set_position("player", camPos.x, camPos.y, camPos.z)
fe3d:model_set_rotation("player", 0.0, yaw, 0.0)
BOOL isPlaying = fe3d:model_is_animation_playing("walk", "player")
IF _isWalking IS <true> AND isPlaying IS <false>:
    fe3d:model_start_animation("walk", "player", -1)
ELIF _isWalking IS <false> AND isPlaying IS <true>:
    fe3d:model_fade_animation("walk", "player", 1)

/// Update running
IF isPlaying IS <true>:
    DEC speed = 1.0
    IF _isRunning IS <true>:
        MUL speed 2.0
    fe3d:model_set_animation_speed("walk", "player", speed)

/// Update axe position, rotation, animation if equipped
IF _inventory[0] NOT "":
    DECR yaw 20.0
    DEC sinRot = math:sin(yaw)
    DEC cosRot = math:cos(yaw)
    MUL sinRot 0.8
    MUL cosRot 0.8
    VEC3 axePos = camPos
    INCR axePos.x sinRot
    INCR axePos.z cosRot
    INCR axePos.y 1.0
    fe3d:model_set_position("axe", axePos.x, axePos.y, axePos.z)
    VEC3 axeRot = fe3d:model_get_rotation("axe")
    EDIT yaw = fe3d:camera_get_yaw()
    NEG yaw
    INCR yaw 90.0
    EDIT axeRot.y = yaw
    fe3d:model_set_rotation("axe", axeRot.x, axeRot.y, axeRot.z)
    BOOL lmb = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
    EDIT isPlaying = fe3d:model_is_animation_playing("axe_cut", "axe")
    IF lmb IS <true> AND isPlaying IS <false>:
        fe3d:model_start_animation("axe_cut", "axe", 1)
