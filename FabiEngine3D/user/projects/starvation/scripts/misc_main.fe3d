78 17
META script_type_update

/// Temporary values
STR currentWorld = fe3d:scene_get_current_id()
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update black overlay
fe3d:image_set_alpha("black", _blackAlpha)
IF _blackAlpha LESS _targetBlackAlpha:
    INCR _blackAlpha 0.0025
ELSE:
    DECR _blackAlpha 0.0025

/// Update travelling
IF currentWorld IS "home":
    STR signID = fe3d:collision_check_camera_model("sign", "", "")
    IF signID NOT "":
        fe3d:scene_load("starvia_outside")
        fe3d:camera_set_position(0.0, 9.0, 140.0)

/// Update crosshair visiblity
IF _isCrafting IS <true> OR _isBuilding IS <true> OR _isInStorage IS <true>:
    fe3d:image_set_visible("crosshair", <false>)
ELSE:
    fe3d:image_set_visible("crosshair", <true>)

/// Update water drinking
fe3d:text_set_visible("drink", <false>)
STR hoveredID = fe3d:raycast_into_model("well", "", <true>)
IF hoveredID NOT "" AND _isBuilding IS <false>:
    DEC height = fe3d:model_get_max_height(hoveredID)
    VEC3 size = fe3d:aabb_get_size("well_display_well")
    DEC wellDistance = fe3d:raycast_into_model_distance("well", "", <true>)
    IF wellDistance IS -1.0:
        EDIT wellDistance = 1000.0
    IF wellDistance LESS _maxReach AND height MORE size.y:
        fe3d:text_set_visible("drink", <true>)
        IF ePressed IS <true>:
            INCR _thirst 0.25

/// Update washing
VEC3 camPos = fe3d:camera_get_position()
DEC waterY = fe3d:water_get_height()
IF camPos.y LESS waterY:
    INCR _hygiene 0.001

/// Check if player wants to open/close survival time
EDIT _wasTimeOpen = _isTimeOpen
BOOL tPressed = fe3d:input_is_key_pressed("KEY_T")
IF tPressed IS <true>:
    IF _isTimeOpen IS <true>:
        EDIT _isTimeOpen = <false>
    ELSE:
        EDIT _isTimeOpen = <true>

/// Update survival time visibility
IF _wasTimeOpen NOT _isTimeOpen OR _survivalTime IS 0:
    fe3d:image_set_visible("scroll_time", _isTimeOpen)
    fe3d:text_set_visible("title_time", _isTimeOpen)
    fe3d:text_set_visible("time", _isTimeOpen)

/// Update survival time
DEC elapsedMS = fe3d:timer_stop()
INCR _elapsedMS elapsedMS
CAST _survivalTime DEC
EDIT _survivalTime = _elapsedMS
MUL _survivalTime 0.001
CAST _survivalTime INT
fe3d:timer_start()

/// Update survival time text
IF _isTimeOpen IS <true>:
    CAST _survivalTime STR
    STR text = misc:concat_strings("You wasted ", _survivalTime)
    EDIT text = misc:concat_strings(text, " seconds of your life!")
    CAST _survivalTime INT
    INT size = misc:get_string_size(text)
    CAST size DEC
    MUL size 0.02
    fe3d:text_set_content("time", text)
    fe3d:text_set_size("time", size, 0.05)
