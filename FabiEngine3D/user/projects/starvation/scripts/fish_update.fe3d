37 30
META script_type_update

/// Temporary values
STR currentWorld = fe3d:scene_get_current_id()
STR toolID = _inventory[_equipIndex]
VEC3 terrainPoint = fe3d:raycast_get_position_on_terrain()
VEC3 camPos = fe3d:camera_get_position()
DEC waterY = fe3d:water_get_height()
BOOL isValid = fe3d:raycast_is_position_on_terrain_valid()

/// Check if throwing is finished
IF _isFishrodPlaying IS <true>:
    BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
    BOOL resumed = fe3d:model_is_animation_playing("fishrod", "fishrod_throw")
    INT index = fe3d:model_get_animation_frame_index("fishrod", "fishrod_throw")
    IF index IS 3 AND _isFishing IS <false>:
        fe3d:model_pause_animation("fishrod", "fishrod_throw")
        EDIT _isFishing = <true>
    ELIF lmbPressed IS <true> AND _isFishing IS <true> AND resumed IS <false>:
        fe3d:model_resume_animation("fishrod", "fishrod_throw")
ELSE:
    EDIT _isFishing = <false>

/// Update fish catching process
IF currentWorld IS "home":
    DEC maxCamY = 33.0
ELIF currentWorld IS "starvia_outside":
    DEC maxCamY = 8.0
BOOL a = (terrainPoint.y LESS waterY AND camPos.y LESS maxCamY AND isValid IS <true>)
fe3d:text_set_visible("fish", <false>)
IF a IS <true>:
    IF _isFishing IS <true>:
        BOOL isPlaying = fe3d:model_is_animation_playing("fishrod", "fishrod_throw")
        IF isPlaying IS <false>:
            INT randomInt = misc:get_random_integer(0, 1500)
            IF randomInt IS 0:
                fe3d:model_resume_animation("fishrod", "fishrod_throw")
                INCR _bag[7] 1
                DECR _durabilities[8] _durabilityHit
    ELIF toolID IS "fishrod" AND _isInvOpen IS <true> AND _isFishrodPlaying IS <false>:
        fe3d:text_set_visible("fish", <true>)
