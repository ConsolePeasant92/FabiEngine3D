47 32
META script_type_update

/// Compose pickuppable items
LIST pickuppables = {"log","stones","bush","plant","mushroom"}
PUSH pickuppables "pickaxe_stone"
PUSH pickuppables "pickaxe_iron"
PUSH pickuppables "axe_stone"
PUSH pickuppables "axe_iron"

/// Pickupabbles hover
fe3d:text_set_visible("pickup", <false>)
INT size = misc:get_list_size("pickuppables")
INT index = 0
STR hoveredID = ""
STR itemName = ""
VEC3 camPos = fe3d:camera_get_position()
LOOP:
    IF index IS size:
        BREAK
    ELSE:
        EDIT hoveredID = fe3d:raycast_into_model(pickuppables[index], "", <true>)
        IF hoveredID NOT "":
            VEC3 itemPos = fe3d:model_get_position(hoveredID)
            DEC distance = math:distance(itemPos, camPos)
            IF distance LESS _maxPickupReach:
                EDIT itemName = pickuppables[index]
                fe3d:text_set_visible("pickup", <true>)
                BREAK
    INCR index 1

/// Tools pickup
BOOL pickaxe = (itemName IS "pickaxe_stone" OR itemName IS "pickaxe_iron")
BOOL axe = (itemName IS "axe_stone" OR itemName IS "axe_iron")
IF pickaxe IS <true>:
    EDIT index = 1
IF axe IS <true>:
    EDIT index = 2
IF pickaxe IS <true> OR axe IS <true>:
    IF _wasPickupPlaying IS <true> AND _isPickupPlaying IS <false>:
        fe3d:model_delete(hoveredID)
        /// Unequip old tool
        IF _inventory[_equipIndex] NOT "":
            fe3d:model_set_visible(_inventory[_equipIndex], <false>)
        /// Equip new tool
        fe3d:model_place(itemName, itemName, 0.0, 0.0, 0.0)
        fe3d:model_set_aabb_responsive(itemName, <false>)
        EDIT _inventory[index] = itemName
        EDIT _equipIndex = index
