51 53
META script_type_update

/// Torch position
BOOL equipped = (_equipIndex IS 7 AND _inventory[_equipIndex] NOT "")
IF equipped IS <true> AND _isInvOpen IS <true>:
    VEC3 torchPos = fe3d:model_get_position("torch")
    VEC3 firePos = torchPos
    INCR torchPos.y 2.0
    INCR firePos.y 1.8
    fe3d:light_set_position("torch", torchPos.x, torchPos.y, torchPos.z)
    fe3d:billboard_set_position("torch", firePos.x, firePos.y, firePos.z)

/// Check if it's nighttime
IF _isDayTime IS <false> AND equipped IS <true> AND _isInvOpen IS <true>:
    fe3d:light_set_visible("torch", <true>)
    fe3d:billboard_set_visible("torch", <true>)
    DECR _durabilities[7] 0.0001
ELSE:
    fe3d:light_set_visible("torch", <false>)
    fe3d:billboard_set_visible("torch", <false>)

/// Black intro fading
fe3d:image_set_alpha("black", _blackAlpha)
IF _blackAlpha LESS _targetBlackAlpha:
    INCR _blackAlpha 0.0025
ELSE:
    DECR _blackAlpha 0.0025

/// Travelling to Starvia
STR currentWorld = fe3d:scene_get_current_id()
IF currentWorld IS "home":
    STR signID = fe3d:collision_check_camera_model("sign", "", "")
    IF signID NOT "":
        fe3d:scene_load("starvia_outside")
        fe3d:camera_set_position(0.0, 9.0, 140.0)

/// Update crosshair visiblity
IF _isCrafting IS <true> OR _isBuilding IS <true>:
    fe3d:image_set_visible("crosshair", <false>)
ELSE:
    fe3d:image_set_visible("crosshair", <true>)

/// Update water drinking
fe3d:text_set_visible("drink", <false>)
STR hoveredID = fe3d:raycast_into_model("well", "", <true>)
IF hoveredID NOT "" AND _isBuilding IS <false>:
    DEC height = fe3d:model_get_max_height(hoveredID)
    VEC3 size = fe3d:aabb_get_size("well_display_well")
    DEC distance = fe3d:raycast_into_model_distance("well", "", <true>)
    IF distance IS -1.0:
        EDIT distance = 1000.0
    IF distance LESS _maxReach AND height MORE size.y:
        fe3d:text_set_visible("drink", <true>)
        BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")
        IF ePressed IS <true>:
            EDIT _thirst = 1.0
