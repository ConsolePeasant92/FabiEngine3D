20 48
META script_type_update

/// Check if a built building is selected
INT listSize = misc:get_list_size("_buildables")
INT index = 0
STR hoveredID = ""
LOOP:
    IF index IS listSize:
        BREAK
    EDIT hoveredID = fe3d:raycast_into_model(_buildables[index], "", <true>)
    IF hoveredID NOT "":
        BREAK
    INCR index 1

/// Update building text
STR toolID = _inventory[_equipIndex]
BOOL isHovered = <false>
fe3d:text_set_visible("build", <false>)
IF toolID IS "hammer_stone" OR toolID IS "hammer_iron":
    IF _isInvOpen IS <true> AND hoveredID NOT "":
        VEC3 camPos = fe3d:camera_get_position()
        VEC3 buildingPos = fe3d:model_get_position(hoveredID)
        DEC distance = math:distance(buildingPos, camPos)
        IF distance LESS 100.0:
            fe3d:text_set_visible("build", <true>)
            EDIT isHovered = <true>

/// Update building growth
IF _wasHammerPlaying IS <true> AND _isHammerPlaying IS <false> AND isHovered IS <true>:
    DEC height = fe3d:model_get_max_height(hoveredID)
    IF _buildIndex IS 0:
        VEC3 size = fe3d:aabb_get_size("small_house_display_front_left")
    IF _buildIndex IS 1:
        VEC3 size = fe3d:aabb_get_size("medium_house_display_front")
    IF _buildIndex IS 2:
        VEC3 size = fe3d:aabb_get_size("big_house_display_back")
    IF _buildIndex IS 3:
        VEC3 size = fe3d:aabb_get_size("storage_display_main")
    IF _buildIndex IS 4:
        VEC3 size = fe3d:aabb_get_size("well_display_well")
    IF _buildIndex IS 5:
        VEC3 size = fe3d:aabb_get_size("pen_display_front")
    DEC heightIncrease = size.y
    MUL heightIncrease 0.1
    INCR height heightIncrease
    fe3d:model_set_max_height(hoveredID, height)
