14 14
META script_type_update

/// Temporary values
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update storage hover
fe3d:text_set_visible("storage", <false>)
BOOL isHovered = <false>
STR hoveredID = fe3d:raycast_into_model("storage", "door", <true>)
IF hoveredID NOT "" AND _isBuilding IS <false>:
    DEC distance = fe3d:raycast_into_model_distance("storage", "", <true>)
    IF distance IS -1.0:
        EDIT distance = 1000.0
    IF distance LESS _maxReach AND _isInStorage IS <false>:
        IF _isToolPlaying IS <false>:
            EDIT isHovered = <true>
            fe3d:text_set_visible("storage", <true>)

/// Update storage menu open & close
IF ePressed IS <true>:
    IF isHovered IS <true> AND _isInStorage IS <false>:
        EDIT _isInStorage = <true>
        fe3d:cursor_show()
    ELIF _isInStorage IS <true>:
        EDIT _isInStorage = <false>
        fe3d:cursor_hide()

/// Update storage menu interaction
IF _isInStorage IS <true>:
    STR buttonID = ""
    DEC buttonWidth = 0.0
    DEC buttonHeight = 0.0
    DEC buttonX = 0.0
    DEC buttonY = 0.0
    DEC cursorX = 0.0
    DEC cursorY = 0.0
    DEC xDifference = 0.0
    DEC yDifference = 0.0
    INT index = 0
    INT size = misc:get_list_size("_storageButtonIDs")
    LOOP:
        IF index IS size:
            BREAK
        ELSE:
            EDIT buttonID = _storageButtonIDs[index]
            EDIT buttonWidth = fe3d:image_get_width(buttonID)
            MUL buttonWidth 0.5
            EDIT buttonHeight = fe3d:image_get_height(buttonID)
            MUL buttonHeight 0.5
            EDIT buttonX = fe3d:image_get_position_x(buttonID)
            EDIT buttonY = fe3d:image_get_position_y(buttonID)
            EDIT cursorX = fe3d:cursor_get_position_x()
            EDIT cursorY = fe3d:cursor_get_position_y()
            EDIT xDifference = buttonX
            DECR xDifference cursorX
            EDIT xDifference = math:abs(xDifference)
            EDIT yDifference = buttonY
            DECR yDifference cursorY
            EDIT yDifference = math:abs(yDifference)
            IF xDifference LESS buttonWidth AND yDifference LESS buttonHeight:
                fe3d:image_set_color(buttonID, 0.0, 0.5, 0.0)
            ELSE:
                fe3d:image_set_color(buttonID, 1.0, 1.0, 1.0)
            INCR index 1
