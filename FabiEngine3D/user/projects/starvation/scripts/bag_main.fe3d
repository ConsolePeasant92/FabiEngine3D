48 21
META script_type_update

/// Check if player wants to open/close bag
BOOL bPressed = fe3d:input_is_key_pressed("KEY_B")
IF bPressed IS <true>:
    IF _isBagOpen IS <true>:
        EDIT _isBagOpen = <false>
        fe3d:image_set_visible("bag_open", <false>)
        fe3d:image_set_visible("scroll_bag", <false>)
        fe3d:image_set_visible("bag_closed", <true>)
    ELSE:
        EDIT _isBagOpen = <true>
        fe3d:image_set_visible("bag_open", <true>)
        fe3d:image_set_visible("scroll_bag", <true>)
        fe3d:image_set_visible("bag_closed", <false>)

/// Update bag items visibility
fe3d:image_set_visible("wood_bag", _isBagOpen)
fe3d:text_set_visible("wood_count_bag", _isBagOpen)
fe3d:image_set_visible("stone_bag", _isBagOpen)
fe3d:text_set_visible("stone_count_bag", _isBagOpen)
fe3d:image_set_visible("clay_bag", _isBagOpen)
fe3d:text_set_visible("clay_count_bag", _isBagOpen)
fe3d:image_set_visible("seeds_bag", _isBagOpen)
fe3d:text_set_visible("seeds_count_bag", _isBagOpen)
fe3d:image_set_visible("wheat_bag", _isBagOpen)
fe3d:text_set_visible("wheat_count_bag", _isBagOpen)
fe3d:image_set_visible("flour_bag", _isBagOpen)
fe3d:text_set_visible("flour_count_bag", _isBagOpen)
fe3d:image_set_visible("bread_bag", _isBagOpen)
fe3d:text_set_visible("bread_count_bag", _isBagOpen)
fe3d:image_set_visible("fish_bag", _isBagOpen)
fe3d:text_set_visible("fish_count_bag", _isBagOpen)
fe3d:image_set_visible("meat_bag", _isBagOpen)
fe3d:text_set_visible("meat_count_bag", _isBagOpen)
fe3d:image_set_visible("iron_bag", _isBagOpen)
fe3d:text_set_visible("iron_count_bag", _isBagOpen)
fe3d:image_set_visible("coin_bag", _isBagOpen)
fe3d:text_set_visible("coin_count_bag", _isBagOpen)

/// Update bag items text
INT listSize = misc:get_list_size("_bag")
INT index = 0
INT itemCount = 0
INT strSize = 0
DEC weight = 0.0
EDIT _totalBagWeight = 0.0
LOOP:
    IF index IS listSize:
        BREAK
    ELSE:
        /// Calculate total bag weight
        EDIT itemCount = _bag[index]
        CAST itemCount DEC
        EDIT weight = itemCount
        MUL weight _bagWeights[index]
        INCR _totalBagWeight weight
        CAST itemCount INT
        CAST itemCount STR
        /// Update amount of digits
        EDIT strSize = misc:get_string_size(itemCount)
        IF strSize IS 1:
            EDIT itemCount = misc:concat_strings("00", itemCount)
        ELIF strSize IS 2:
            EDIT itemCount = misc:concat_strings("0", itemCount)
        fe3d:text_set_content(_bagTextIDs[index], itemCount)
        CAST itemCount INT
        INCR index 1

/// Update bag icon color
DEC totalWeight = _totalBagWeight
DIV totalWeight _maxBagWeight
DEC inverseColor = 1.0
DECR inverseColor totalWeight
fe3d:image_set_color("bag_open", 1.0, inverseColor, inverseColor)

/// Update bag weight text
fe3d:text_set_visible("bag_weight", _isBagOpen)
EDIT totalWeight = _totalBagWeight
CAST totalWeight STR
EDIT totalWeight = misc:get_string_part(totalWeight, 0, 4)
EDIT totalWeight = misc:concat_strings(totalWeight, "KG")
fe3d:text_set_content("bag_weight", totalWeight)
