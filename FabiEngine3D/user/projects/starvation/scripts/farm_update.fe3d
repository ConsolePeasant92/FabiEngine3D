29 16
META script_type_update

/// Temporary values
VEC3 camPos = fe3d:camera_get_position()
DEC feetY = fe3d:terrain_get_pixel_height(camPos.x, camPos.z)
DEC waterY = fe3d:water_get_height()

/// Update plowing text
fe3d:text_set_visible("plow", <false>)
BOOL isHovered = <false>
STR toolID = _inventory[_equipIndex]
IF feetY MORE waterY AND camPos.y LESS 34.0:
    IF toolID IS "hoe" AND _isInvOpen IS <true>:
        fe3d:text_set_visible("plow", <true>)
        EDIT isHovered = <true>

/// Check if plowing animation was finished
IF _wasHoePlaying IS <true> AND _isHoePlaying IS <false>:
    IF isHovered IS <true>:
        INT newID = misc:get_unique_integer(0, 1000)
        CAST newID STR
        EDIT newID = misc:concat_strings("corn", newID)
        fe3d:model_place(newID, "corn", camPos.x, feetY, camPos.z)
        fe3d:model_set_aabb_collision_responsive(newID, <false>)
        STR aabbID = misc:concat_strings(newID, "_corn")
        VEC3 size = fe3d:aabb_get_size(aabbID)
        MUL size.y 0.025
        fe3d:model_set_max_height(newID, size.y)

/// Update corn growing
LIST cornIDs = fe3d:model_find_ids("corn")
INT length = misc:get_list_size("cornIDs")
INT index = 0
STR cornID = ""
STR cornAabbID = ""
VEC3 cornSize = [0.0 0.0 0.0]
DEC height = 0.0
LOOP:
    IF index IS length:
        BREAK
    ELSE:
        EDIT cornID = cornIDs[index]
        EDIT cornAabbID = misc:concat_strings(cornID, "_corn")
        EDIT cornSize = fe3d:aabb_get_size(cornAabbID)
        EDIT height = fe3d:model_get_max_height(cornID)
        IF height LESS cornSize.y:
            INCR height 0.001
            fe3d:model_set_max_height(cornID, height)
        INCR index 1
