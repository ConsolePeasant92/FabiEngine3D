84 61
META script_type_update

/// Skeleton position & rotation
VEC3 camPos = fe3d:camera_get_position()
DEC yaw = fe3d:camera_get_yaw()
NEG yaw
INCR yaw 90.0
DECR camPos.y 2.6
fe3d:model_set_position("player", camPos.x, camPos.y, camPos.z)
fe3d:model_set_rotation("player", 0.0, yaw, 0.0)

/// Walking animation
BOOL isWalkPlaying = fe3d:model_is_animation_playing("walk", "player")
IF _toolPlaying IS <true>:
    IF isWalkPlaying IS <true>:
        fe3d:model_fade_animation("walk", "player", 1)
ELIF _pickupPlaying IS <false>:
    IF _isWalking IS <true> AND isWalkPlaying IS <false>:
        fe3d:model_start_animation("walk", "player", -1)
    ELIF _isWalking IS <false> AND isWalkPlaying IS <true>:
        fe3d:model_fade_animation("walk", "player", 1)
    /// Update running animation
    IF isWalkPlaying IS <true>:
        DEC speed = 1.0
        IF _isRunning IS <true>:
            MUL speed 2.0
        fe3d:model_set_animation_speed("walk", "player", speed)

/// Determine tool animation type
STR animID = ""
IF _equipIndex IS 0:
    EDIT animID = "sword_hit"
    DEC toolOffset = 1.5
ELIF _equipIndex IS 1:
    EDIT animID = "pickaxe_mine"
    DEC toolOffset = 1.25
ELIF _equipIndex IS 2:
    EDIT animID = "axe_cut"
    DEC toolOffset = 1.0
ELIF _equipIndex IS 3:
    EDIT animID = "shovel_dig"
    DEC toolOffset = 1.0
ELIF _equipIndex IS 4:
    EDIT animID = "hoe_plow"
ELIF _equipIndex IS 5:
    EDIT animID = "scythe_reap"
ELIF _equipIndex IS 6:
    EDIT animID = "hammer_build"
ELIF _equipIndex IS 8:
    EDIT animID = "fishrod_throw"

/// Update tool position, rotation, animation if equipped
STR toolID = _inventory[_equipIndex]
IF toolID NOT "" AND animID NOT "" AND _toolPlaying IS <false> AND _isInvOpen IS <true>:
    DECR yaw 40.0
    DEC sinRot = math:sin(yaw)
    DEC cosRot = math:cos(yaw)
    MUL sinRot 0.8
    MUL cosRot 0.8
    VEC3 toolPos = camPos
    INCR toolPos.x sinRot
    INCR toolPos.z cosRot
    INCR toolPos.y toolOffset
    fe3d:model_set_position(toolID, toolPos.x, toolPos.y, toolPos.z)
    /// Rotation
    VEC3 toolRot = fe3d:model_get_rotation(toolID)
    EDIT toolRot.y = fe3d:camera_get_yaw()
    NEG toolRot.y
    INCR toolRot.y 90.0
    fe3d:model_set_rotation(toolID, toolRot.x, toolRot.y, toolRot.z)
    /// Tool animation
    IF animID NOT "" AND _isJumping IS <false> AND _isFalling IS <false>:
        BOOL lmb = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
        BOOL isPlaying = fe3d:model_is_animation_playing(animID, toolID)
        IF lmb IS <true> AND isPlaying IS <false> AND _stamina MORE 9.0:
            fe3d:model_start_animation(animID, toolID, 1)
            DECR _stamina 10.0

/// Pickup animation
IF _isInvOpen IS <false> AND _isJumping IS <false> AND _isFalling IS <false>:
    IF isWalkPlaying IS <false>:
        BOOL f = fe3d:input_is_key_pressed("KEY_F")
        BOOL isPlaying = fe3d:model_is_animation_playing("pickup", "player")
        IF f IS <true> AND isPlaying IS <false>:
            fe3d:model_start_animation("pickup", "player", 1)
