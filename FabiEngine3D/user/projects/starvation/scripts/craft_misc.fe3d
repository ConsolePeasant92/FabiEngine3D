9 30
META script_type_update

/// Update workbench hover
fe3d:text_set_visible("craft", <false>)
BOOL isHovered = <false>
STR hoveredID = fe3d:raycast_into_model("workbench", "", <true>)
IF hoveredID NOT "" AND _isBuilding IS <false>:
    DEC distance = fe3d:raycast_into_model_distance("workbench", "", <true>)
    IF distance IS -1.0:
        EDIT distance = 1000.0
    IF distance LESS _maxReach AND _isCrafting IS <false>:
        EDIT isHovered = <true>
        fe3d:text_set_visible("craft", <true>)

/// Update crafting menu - open & close
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")
IF ePressed IS <true> AND _isBuilding IS <false> AND _isToolPlaying IS <false>:
    IF isHovered IS <true> AND _isCrafting IS <false>:
        EDIT _isCrafting = <true>
        fe3d:cursor_show()
    ELIF _isCrafting IS <true>:
        EDIT _isCrafting = <false>
        fe3d:cursor_hide()

/// Update HUD visibility
fe3d:model_set_visible(_craftables[_craftIndex], _isCrafting)
fe3d:image_set_visible("scroll_craft", _isCrafting)
fe3d:image_set_visible("wood_price_craft", _isCrafting)
fe3d:image_set_visible("stone_price_craft", _isCrafting)
fe3d:image_set_visible("iron_price_craft", _isCrafting)
fe3d:text_set_visible("wood_price_craft", _isCrafting)
fe3d:text_set_visible("stone_price_craft", _isCrafting)
fe3d:text_set_visible("iron_price_craft", _isCrafting)
fe3d:text_set_visible(_craftables[_craftIndex], _isCrafting)
fe3d:billboard_set_visible("craft_confirm", _isCrafting)
fe3d:billboard_set_visible("craft_cancel", _isCrafting)

/// Update price tag
INT woodPrice = _craftablesWoodPrice[_craftIndex]
INT stonePrice = _craftablesStonePrice[_craftIndex]
INT ironPrice = _craftablesIronPrice[_craftIndex]
CAST woodPrice STR
CAST stonePrice STR
CAST ironPrice STR
fe3d:text_set_content("wood_price_craft", woodPrice)
fe3d:text_set_content("stone_price_craft", stonePrice)
fe3d:text_set_content("iron_price_craft", ironPrice)
