23 36
META script_type_update

/// Temporary values
STR currentWorld = fe3d:scene_get_current_id()

/// Update fire billboards outside of Starvia
IF currentWorld IS "starvia_outside":
    CONST LIST billboards = fe3d:billboard_find_ids("fire1")
    INT index = 0
    INT listSize = misc:get_list_size("billboards")
    LOOP:
        IF index IS listSize:
            BREAK
        ELSE:
            fe3d:billboard_set_visible(billboards[index], _areLightsOn)
            INCR index 1

/// Update fire billboards at home
IF currentWorld IS "home":
    CONST LIST billboards = fe3d:billboard_find_ids("fire3")
    VEC3 camPos = fe3d:camera_get_position()
    VEC3 billboardPos = [0.0 0.0 0.0]
    DEC distance = 0.0
    DEC heatFactor = _temperatureFireFactor
    MUL heatFactor 0.5
    INT index = 0
    INT listSize = misc:get_list_size("billboards")
    LOOP:
        IF index IS listSize:
            BREAK
        ELSE:
            /// Fire visibility
            fe3d:billboard_set_visible(billboards[index], _areLightsOn)
            /// Fire heat
            EDIT billboardPos = fe3d:billboard_get_position(billboards[index])
            EDIT distance = math:distance(camPos, billboardPos)
            IF distance LESS _maxCampfireHeatDistance AND _areLightsOn IS <true>:
                INCR _temperature heatFactor
            INCR index 1
