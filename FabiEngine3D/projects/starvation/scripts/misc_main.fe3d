58 12
META script_type_update

/// Temporary values
STR currentWorld = fe3d:scene_get_current_id()
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")
BOOL qPressed = fe3d:input_is_key_pressed("KEY_Q")

/// Update torch
BOOL equipped = (_equipIndex IS 7 AND _inventory[_equipIndex] NOT "")
fe3d:light_set_visible("torch", <false>)
fe3d:billboard_set_visible("torch", <false>)
IF equipped IS <true> AND _isInvOpen IS <true>:
    VEC3 torchPos = fe3d:model_get_position("torch")
    VEC3 firePos = torchPos
    INCR torchPos.y 2.0
    INCR firePos.y 1.0
    fe3d:light_set_position("torch", torchPos.x, torchPos.y, torchPos.z)
    fe3d:billboard_set_position("torch", firePos.x, firePos.y, firePos.z)
    /// Check if it's nighttime
    IF _areLightsOn IS <true> AND _isUnderWater IS <false>:
        fe3d:light_set_visible("torch", <true>)
        fe3d:billboard_set_visible("torch", <true>)
        DEC durabilityHit = _durabilityHit
        MUL durabilityHit 0.01
        DECR _durabilities[7] durabilityHit

/// Update black overlay
fe3d:image_set_alpha("black", _blackAlpha)
IF _blackAlpha LESS _targetBlackAlpha:
    INCR _blackAlpha _blackOverlaySpeed
ELSE:
    DECR _blackAlpha _blackOverlaySpeed

/// Update travelling
IF currentWorld IS "home":
    STR signID = fe3d:collision_check_camera_model("sign", "", "")
    IF signID NOT "" AND ePressed IS <true>:
        fe3d:scene_load("starvia_outside")
        fe3d:camera_set_position(0.0, 9.0, 140.0)

/// Update crosshair visiblity
IF _isCrafting IS <true> OR _isBuilding IS <true> OR _isInStorage IS <true>:
    fe3d:image_set_visible("crosshair", <false>)
ELSE:
    fe3d:image_set_visible("crosshair", <true>)

/// Check if player is allowed to save
BOOL a = (_isInMenu IS <false> AND _isBuilding IS <false>)
BOOL b = (_isToolPlaying IS <false> AND _isPickupPlaying IS <false>)
BOOL c = (_isTalking IS <false> AND _isCrafting IS <false>)
BOOL d = (_isSleeping IS <false> AND _isInStorage IS <false>)
EDIT _isAllowedToSave = (a IS <true> AND b IS <true> AND c IS <true> AND d IS <true>)

/// Check if player wants to quit
IF qPressed IS <true>:
    IF _isAllowedToSave IS <true>:
        fe3d:game_stop()
    ELSE:
        PASS
